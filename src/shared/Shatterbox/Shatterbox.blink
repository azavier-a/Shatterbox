struct PartProperties {
	Color : Color3,
	Material : u16(256..2311),
	Transparency : f16,
	Reflectance : f16
}

struct Box {
	CFrame : CFrame,
	Size : vector
}

struct Destructible {
	GridSize : f16?,
	Tags : string[]
}

struct PartInstance {
	..Box,

	..PartProperties,

	..Destructible
}

struct Puppet {
	GUID : string,

	..Box,

	..PartProperties
}

struct Dirtied {
	OriginalPart : Box,
	Children : Box[],
	
	..Destructible,

	..PartProperties
}

struct OpDirty {
	ID : string,
	Dirties : Dirtied[]
}

struct DestructionParamsType {
	Shape : u8(0..4),
	CFrame : CFrame,
	Size : vector,
	
	FilterTagged : string[]?,
	GridSize : f32?,
	CleanupDelay : f32?,
	SkipEncapsulatedVoxels : boolean?,
	ID : string,
	
	OnVoxelDestruct : string?,
	DestructParameters : unknown[]
}

struct PuppetState {
	GUID : string,
	CFrame : CFrame 
}



event SignalUndo {
	From: Server,
	Type: Reliable,
	Call: ManyAsync,
	Data: string
}

event SignalClearQueue {
	From: Server,
	Type: Reliable,
	Call: ManyAsync
}

event SignalReset {
	From: Server,
	Type: Reliable,
	Call: ManyAsync
}



event MapReplicationCompleted {
	From: Client,
	Type: Reliable,
	Call: SingleAsync,
	Data: string
}



event CreatePuppets {
	From: Server,
	Type: Reliable,
	Call: ManyAsync,
	Data: Puppet[]
}

event ReplicatePuppetStates {
	From: Server,
	Type: Reliable,
	Call: ManyAsync,
	Data: PuppetState[]
}

event DestroyPuppet {
	From: Server,
	Type: Reliable,
	Call: ManyAsync,
	Data: string
}



event ReplicateOpDirties {
	From: Server,
	Type: Reliable,
	Call: ManyAsync,
	Data: OpDirty[]
}

event ReplicateDestruction {
	From: Server,
	Type: Reliable,
	Call: ManyAsync,
	Data: (DestructionParamsType, PartInstance[]?)
}

event ReplicateImaginary {
	From: Server,
	Type: Reliable,
	Call: ManyAsync,
	Data: (DestructionParamsType, PartInstance[]?)
}